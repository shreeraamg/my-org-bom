plugins {
  id 'java-platform'
  id 'maven-publish'
}

group = 'com.my-org'
version = '1.0.0'

javaPlatform {
  allowDependencies()
}

repositories {
  mavenCentral()
}

dependencies {
  constraints {
    api 'com.auth0:java-jwt:4.4.0'
    api 'com.google.guava:guava:33.4.7-jre'
    api 'io.confluent.ksql:ksqldb-api-client:8.0.0'
    api 'org.apache.commons:commons-collections4:4.5.0'
    api 'org.json:json:20250107'
    api 'org.mapstruct:mapstruct:1.6.2'
    api 'org.mapstruct:mapstruct-processor:1.6.2'
    api 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    api 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
  }
}

def githubUser = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
def githubToken = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")

publishing {
  publications {
    mavenBom(MavenPublication) {
      from components.javaPlatform
    }
  }

  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/${githubUser}/my-org-bom")
      credentials {
        username = githubUser
        password = githubToken
      }
    }
  }
}