plugins {
  id 'java-platform'
  id 'maven-publish'
}

group = 'com.my-org'

javaPlatform {
  allowDependencies()
}

repositories {
  mavenCentral()
  maven {
    url = uri("https://packages.confluent.io/maven/")
  }
}

dependencies {
  constraints {
    api 'com.auth0:java-jwt:4.5.0'
    api 'com.google.guava:guava:33.4.8-jre'
    api 'io.confluent.ksql:ksqldb-api-client:8.0.0'
    api 'org.apache.commons:commons-collections4:4.5.0'
    api 'org.json:json:20250517'
    api 'org.mapstruct:mapstruct:1.6.3'
    api 'org.mapstruct:mapstruct-processor:1.6.3'
    api 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    api 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.10'
  }
}

def githubUser = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
def githubToken = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")

publishing {
  publications {
    mavenBom(MavenPublication) {
      from components.javaPlatform
    }
  }

  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/shreeraamg/my-org-bom")
      credentials {
        username = githubUser
        password = githubToken
      }
    }
  }
}